/*
	Capitulo 81 de Arduino desde cero en Español.
	Programa que permite el control de reproducción de audio de un módulo DFPlayer Mini mediante dos
	pulsadores, uno para pausa/reproducción y otro para iniciar un anuncio (archivo específico mp3).
	Requiere librería: DFRobotDFPlayerMini

	Autor: bitwiseAr  
	https://www.youtube.com/@BitwiseAr

*/

#include "DFRobotDFPlayerMini.h"	// incluye librería específica para módulo DFPlayer Mini
#include <SoftwareSerial.h>		// incluye librería SoftwareSerial para función UART

#define PULSADOR 9			// macro para PULSADOR con número 9
#define PULSADOR_ANUNCIO 8		// macro para PULSADOR_ANUNCIO con número 8

SoftwareSerial softSerial(10, 11);	// objeto softSerial para UART en pines 10 y 11

DFRobotDFPlayerMini reproductor;	// objeto reproductor del tipo DFRobotDFPlayerMini

bool reproduciendo = true;		// variable de control para el pulsador con doble función

void setup()
{
softSerial.begin(9600);			// inicializa softSerial a 9600 bps
Serial.begin(9600);			// inicializa monitor serie a 9600 bps
pinMode(PULSADOR, INPUT_PULLUP);		// pin de PULSADOR como entrada con resistencia de pull-up
pinMode(PULSADOR_ANUNCIO, INPUT_PULLUP);	// pin de PULSADOR_ANUNCIO como entrada con resistencias de pul-up
  
if (!reproductor.begin(softSerial, true, false)) {	// si falla la inicialización del módulo
    Serial.println("Error de inicialización.");		// muestra mensaje de error en monitor serie
    while(true);					// bucle infinito detiene el flujo del programa
}
  Serial.println("Reproductor preparado.");	// con inialización exitosa muestra mensaje por monitor serie
  delay(1000);					// demora de 1 seg. para dar tiempo al módulo a finalizar inicialización
  
  reproductor.volume(15);			// volumen al 50 por ciento (rango 0-30)
  reproductor.enableLoopAll();			// habilita reproducción en bucle completa
}

void loop()
{
  if (digitalRead(PULSADOR) == LOW && reproduciendo == true) {	// si se ha presionado el boton y se está reproduciendo
    reproductor.pause();					// pausa la reproducción
    Serial.println("Pausa");					// muestra mensaje en monitor serie
    reproduciendo = false;					// cambia variable reproduciendo por falso
    delay(500);							// demora a modo de antirebote
  } else if (digitalRead(PULSADOR) == LOW && reproduciendo == false){	// si se ha presionado el boton y no se está reproduciendo
    reproductor.start();						// inicia la reproducción
    Serial.println("Reanudando");					// muestra mensaje en monitor serie
    reproduciendo = true;						// cambia variable reproduciendo por verdadero
    delay(500);								// demora a modo de antirebote
  }
  
  if (digitalRead(PULSADOR_ANUNCIO) == LOW) {		// si se ha presionado el boton de Anuncio
      reproductor.advertise(1);				// reproduce el primer archivo en carpeta "advert"
      Serial.println("Anuncio");			// muestra mensaje en monitor serie
      delay(500);					// demora a modo de antirebote
  }

}
